




OnPluginLoad.Menu {
    if ( RadioName == "" ) { RadioName = "Guest" }
    ReadMeTexts     = "【説明】\n\_a[OnReadMeList]このプラグインの使い方\_a\n"
}
OnPluginUnload.Menu {
    ERASEVAR( "ReadMeTexts" )
    ERASEVAR( "RadioTextDraft" )
}


OnMenuExec {

    res_script_option = "notranslate"
    MainContents()
    //WeekRadio()
}



MainContents {
    "\b[2]\_q\n[half]/
    %( WeatherMenu          )\n/
    %( RadioBoard           )\n/
    %( UserData             )/
    %( SaveClickContent     )\n/
    %( GuestPersonality     )\n/
    %( ReadMeTexts          )\n/
    %( BGMtext              )\n/
	\n[half]\_q\x[noclear]\e"
}


//送信テキストのラジオ化テストと問題がなければ送信する。
GuestPersonality {
    "【寄稿する】\n/
    \_a[OnContributionTestUrlDialog]URLをテストする。\_a\n/
    \_a[OnContributionTestDialog]単発原稿をテストする。\_a\n/
    \_a[OnContribution]単発原稿を送信する。\_a\n/
    "
}

OnContributionTestUrlDialog {
    "\_qテストしたいURLを挿入してください。\n音が出ますので、バルーンが出現するゴーストの設定から\n音を鳴らさないのチェックを外しておいてください。\n\nURLの場合は改行ごとに\\w3と改行が挿入されます。\![open,inputbox,OnContributionTestUrl]\_q"
}
OnContributionTestUrl {
    _url = reference[0]
    //発声
    FUNCTIONEX("./dll/proxy_ex.dll" ,  "/charset" , "UTF-8"  )
    _text = FUNCTIONEX( "./dll/proxy_ex.dll" , "./getHtmlOne.exe" , _url ) 
    if ( _text == "" ){
        "何も読み取れませんでした。"
    } else {
        TalkRadioText( _text )
    }
}

OnContributionTestDialog {
    "\_qテストしたいテキストを挿入してください。\n音が出ますので、バルーンが出現するゴーストの設定から\n音を鳴らさないのチェックを外しておいてください。\![open,inputbox,OnContributionTest]\_q"
}
//テストテキストを表示し、
//下書きを保存する。
OnContributionTest {
    RadioTextDraft = reference[0]
    //発声
    TalkRadioText( RadioTextDraft )
}

//最後にテスト実行した内容を保存しておけばいいか。
OnContribution {
    _Guest.Url = "https://script.google.com/macros/s/AKfycbyFQO_ty4eLOIhhRTHfjLHk1INEqK_2P3k8BWNdAl_JoBFvV9dxpiIdSiB0AW9QzcP7GQ/exec"
    if ( RadioName == "") {
        RN = "Guest"
    } else {
        RN = RadioName
    }

    _text       = RadioTextDraft
    ERASEVAR( "RadioTextDraft" )

    //送信用に変換
    _text       = CheckRadioText( _text )
    //こっち側だけescapeしないと正常に届かないな。
    //[をescapeすると向こうで\[で残る。
    _text       = REPLACE( _text , "]" , "\]" )

    if ( _text != "" ){
        "\![execute,http-POST,%(_Guest.Url),--param=message_body=%(BootCheck);%(RN);%(_text),--async=OnWebClapModoki,--timeout=5]送信しました。"
    } else {
        MainContents()
    }
}





RadioBoard {

    //hagaki
    _Mon.HagakiLink   ="\_a[OnMakeHagaki,%( Mon.Person   ),%( Mon.Hagaki    )]"+ ExistHagakiUrl( Mon.Hagaki   ) +"\_a\n"
    _Tues.HagakiLink  ="\_a[OnMakeHagaki,%( Tues.Person  ),%( Tues.Hagaki   )]"+ ExistHagakiUrl( Tues.Hagaki  ) +"\_a\n"
    _Wednes.HagakiLink="\_a[OnMakeHagaki,%( Wednes.Person),%( Wednes.Hagaki )]"+ ExistHagakiUrl( Wednes.Hagaki) +"\_a\n"
    _Thurs.HagakiLink ="\_a[OnMakeHagaki,%( Thurs.Person ),%( Thurs.Hagaki  )]"+ ExistHagakiUrl( Thurs.Hagaki ) +"\_a\n"
    _Fri.HagakiLink   ="\_a[OnMakeHagaki,%( Fri.Person   ),%( Fir.Hagaki    )]"+ ExistHagakiUrl( Fir.Hagaki   ) +"\_a\n"
    _Satur.HagakiLink ="\_a[OnMakeHagaki,%( Satur.Person ),%( Satur.Hagaki  )]"+ ExistHagakiUrl( Satur.Hagaki ) +"\_a\n"
    _Sun.HagakiLink   ="\_a[OnMakeHagaki,%( Sun.Person   ),%( Sun.Hagaki    )]"+ ExistHagakiUrl( Sun.Hagaki   ) +"\_a\n"

    //atagにして時間変更できるように。
    _Sun.Schedule   =" \_a[OnChangeOnAir,0]" +TOSTR(    Sun.HOUR ) + ":" + TOSTR( Sun.MINUTES    ) + "\_a ～ " 
    _Mon.Schedule   =" \_a[OnChangeOnAir,1]" +TOSTR(    Mon.HOUR ) + ":" + TOSTR( Mon.MINUTES    ) + "\_a ～ "
    _Tues.Schedule  =" \_a[OnChangeOnAir,2]" +TOSTR(   Tues.HOUR ) + ":" + TOSTR( Tues.MINUTES   ) + "\_a ～ "
    _Wednes.Schedule=" \_a[OnChangeOnAir,3]" +TOSTR( Wednes.HOUR ) + ":" + TOSTR( Wednes.MINUTES ) + "\_a ～ "
    _Thurs.Schedule =" \_a[OnChangeOnAir,4]" +TOSTR(  Thurs.HOUR ) + ":" + TOSTR( Thurs.MINUTES  ) + "\_a ～ "
    _Fri.Schedule   =" \_a[OnChangeOnAir,5]" +TOSTR(    Fri.HOUR ) + ":" + TOSTR( Fri.MINUTES    ) + "\_a ～ "
    _Satur.Schedule =" \_a[OnChangeOnAir,6]" +TOSTR(  Satur.HOUR ) + ":" + TOSTR( Satur.MINUTES  ) + "\_a ～ "

	"【放送予定】\n/
	日曜日 :"+_Sun.Schedule   +ExistHomePage( Sun.Person   ,Sun.HomePage   )+Space(Sun.Person)   +   _Sun.HagakiLink+"/
	月曜日 :"+_Mon.Schedule   +ExistHomePage( Mon.Person   ,Mon.HomePage   )+Space(Mon.Person)   +   _Mon.HagakiLink+"/
	火曜日 :"+_Tues.Schedule  +ExistHomePage( Tues.Person  ,Tues.HomePage  )+Space(Tues.Person)  +  _Tues.HagakiLink+"/
	水曜日 :"+_Wednes.Schedule+ExistHomePage( Wednes.Person,Wednes.HomePage)+Space(Wednes.Person)+_Wednes.HagakiLink+"/
	木曜日 :"+_Thurs.Schedule +ExistHomePage( Thurs.Person ,Thurs.HomePage )+Space(Thurs.Person) + _Thurs.HagakiLink+"/
	金曜日 :"+_Fri.Schedule   +ExistHomePage( Fri.Person   ,Fri.HomePage   )+Space(Fri.Person)   +   _Fri.HagakiLink+"/
	土曜日 :"+_Satur.Schedule +ExistHomePage( Satur.Person ,Satur.HomePage )+Space(Satur.Person) + _Satur.HagakiLink+"/
    "
}



//設定されている時間を変更したい。
//ダイアログを出して時間と分を指定できるようにする必要がある。
OnChangeOnAir {
    SetWeek       =     reference[0]
    "4桁の半角数字で時間を入力してね。\n【例】2230 -> 22時30分\![open,inputbox,OnInputTimeSchedule]"
}

//直接evalに流すのはよくない。
//四桁数字を受け取って二桁に分割する。
OnInputTimeSchedule {
    _time       = TOSTR( reference[0] )
    _CheckWeek  = SetWeek
    ERASEVAR( "SetWeek" )

    //_time = TOSTR( "1234" )
    if RE_MATCH( _time , '[0-9]...' ) {

        _hour   = SUBSTR( _time , 0 , 2)
        _min    = SUBSTR( _time , 2 , 2)

        if ( _hour >= 24 || _min >= 60 ) {
            "ちょっと数字が大きいかな。"

        } else {
            _week = ""
            if _CheckWeek == 0 {
                _week = "Sun"

            } elseif _CheckWeek == 1 {
                _week = "Mon"

            } elseif _CheckWeek == 2 {
                _week = "Tues"

            } elseif _CheckWeek == 3 {
                _week = "Wednes"

            } elseif _CheckWeek == 4 {
                _week = "Thurs"

            } elseif _CheckWeek == 5 {
                _week = "Fri"

            } else {
                _week = "Satur"
            }
            EVAL( "%(_week).HOUR      =   _hour" )
            EVAL( "%(_week).MINUTES   =   _min" )
            "%( _hour )時%( _min )分に設定したよ。"
        }
    } else {
        "設定できなかったよ。"
    }
}


//宛先にHomePageがあればリンク付きにするか。
//無ければ名前だけを返そう。
ExistHomePage {
    _man        = _argv[0]
    _homepage   = _argv[1]
    if ( _homepage == "" ) {
        if ( _man == "" ) {
            "\_a[OnOpenHomePageDialog,https://ukadon.shillest.net/@ambergon]募集中\_a"
        } else {
            _man
        }
    } else {
        "\_a[OnOpenHomePageDialog,%(_homepage)]%(_man)\_a"
    }
}

OnOpenHomePageDialog {
    _url = reference[0]
    "\_q\_a[OnOpenHomePage,%(_url)]リンク : %(_url) を開きます。よろしいですか。\_q"
}
OnOpenHomePage {
    _url = reference[0]
    "\j[%(_url)]"
}


UserData {
	"【あなたの情報】\n/
    貴方のラジオネーム : %(RadioName)\n/
    \_a[OnChangeRadioNameDialog]ラジオネームを変更する。\_a\n"
}
SaveClickContent {
    _OldLinkText = ""
    if ( alink_URL != "" ) { _OldLinkText = "【取得項目】\n\_a[OnOpenLinkDialog]%(alink_TITLE)\_a\n" }
    _OldLinkText
}


//Aタグを使うと逃げれてしまう問題があるな。
//なので、ここで発言はさせない。
OnAnchorSelectEx {
    alink_TITLE = reference[0]
    alink_URL   = reference[1]
}
OnOpenLinkDialog {
    "\_q\_a[OnOpenLink]リンク : %(alink_URL) を開きます。よろしいですか。\_q"
}
OnOpenLink {
    "\j[%(alink_URL)]"
    alink_TITLE = ""
    alink_URL   = ""
}


//ラジオネーム変更関数群
OnChangeRadioNameDialog {
    "\_q現在の貴方のラジオネーム : %(RadioName) \![open,inputbox,OnChangeRadioName]\_q"
}
OnChangeRadioName {
    RadioName  = reference[0]
    "\_q現在の貴方のラジオネーム : %(RadioName)"
}



//14文字を超える場合は特に何もしない。
//葉書を送るのスペース埋め
Space {
    _str = ""
    _len = GETSTRBYTES( _argv[0])
    _check = 14 - TOINT(_len)
    if ( 0 < _check ) {
        for _i = 0 ; _i < _check ; _i++ {
            _str = _str + " "
        }
    }
    _str
}


CheckRadioText {
    _SendText  = _argv[0]
    //送信関係
    _SendText  = REPLACE( _SendText , ";" , "" )
    _SendText  = REPLACE( _SendText , "," , "" )
    _SendText  = REPLACE( _SendText , "?" , "？" )

    _SendText  = REPLACE( _SendText , "'" , "" )
    _SendText  = REPLACE( _SendText , '"' , "" )

    _SendText  = REPLACE( _SendText , '|' , "" )
    //%変数名でそのまま呼び出せてしまう。
    _SendText  = REPLACE( _SendText , "#" , "" )
    _SendText  = REPLACE( _SendText , "{" , "" )
    _SendText  = REPLACE( _SendText , "}" , "" )
    _SendText  = REPLACE( _SendText , "(" , "" )
    _SendText  = REPLACE( _SendText , ")" , "" )

    //GAS側で、スペースが+に置き換わってしまうので適当に使いやすく。
    _SendText  = REPLACE( _SendText , "　" , " " )
    _SendText  = REPLACE( _SendText , " " , "_" )

    //除去後に何かが完成するのは気に食わんので_
    _SendText  = REPLACE( _SendText , "%" , "パーセント" )
    _SendText  = RE_REPLACE( _SendText , "\\j\[.*?\]" , "_" )
    _SendText  = RE_REPLACE( _SendText , "\\!\[.*?\]" , "_" )
    _SendText
}



TalkRadioText {
    _text       = _argv[0]

    //ネットから落としたテキストはこの段階でサニタイズが入るようにするか。
    _text       = CheckRadioText( _text )

    //空送信・空白送信除去
    _CheckText  = REPLACE( _text , " " , "" )
    _CheckText  = REPLACE( _CheckText , "　" , "" )

    if ( _CheckText != "" ) {
        _weatherText = GetWeather()
        //_fileBGM     = Music.Path

        //クリック等バルーンを消せないように。
        _Option.RegistClose         = "\![enter,nouserbreakmode]"
        _Option.RegistClose.END     = "\![leave,nouserbreakmode]"

        ////他のイベント通知で割り込まれたりしないように。
        //ghostを優先したいので切る。
        //問題は、musicのstopが発声せずにループが起きる。
        //otherghosttalkで検知するか。
        //_Option.RegistInterrupt      = "\t"
        //_Option.RegistInterrupt.END  = ""

        _Options        = _Option.RegistClose     
        _Options.END    = _Option.RegistClose.END 

        _SoundBefore    = _Options      + "\b[-1]" + SoundStart() + "\b[2]"
        _SoundAfter     = SoundStop() + _Options.END

        _talk            =  _SoundBefore + _text + "\1\b[2]\w9最後に天気のお知らせだよ。\n\w3" + _weatherText + "\0" + _SoundAfter + "\e"
        _talk
        
        res_marker = '【RadioMidnightのお時間】'
        //表示途中のバルーンを食わないように。
        //選択肢待機とかは食ってしまうので完ぺきではない。
        //"\![get,property,OnAAA,currentghost.status]"
        //を試してみたが、このスクリプトを呼ぶこと自体が実行中になってしまい、常にtalking
        res_script_option = "nobreak,notranslate"
    } else {
        MainContents()
    }
}







